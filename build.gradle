plugins {
    id 'java'
    id 'io.quarkus'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'eclipse'
    id "org.kordamp.gradle.jandex" version "1.1.0"
    id 'antlr'
}

apply from: 'buildscripts/code-analysis.gradle'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-container-image-jib'
    implementation 'io.quarkus:quarkus-vertx'
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-grpc'

    implementation 'com.google.protobuf:protobuf-java-util'

    // Java Parsing
    implementation 'com.github.javaparser:javaparser-symbol-solver-core:3.27.0'

    // ANTLR
    antlr 'org.antlr:antlr4:4.13.2'
    implementation 'org.antlr:antlr4-runtime:4.13.2'

    // Git
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.3.0.202506031305-r'

    // otel
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp-logs:1.22.0-alpha'
    implementation 'io.opentelemetry:opentelemetry-proto:1.7.1-alpha'

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.grpc:grpc-testing:1.50.2'
}

group 'net.explorviz'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        java {
            srcDirs 'build/classes/java/quarkus-generated-sources/grpc'
            srcDirs 'build/generated-src/antlr/main'
        }
    }
}

// ANTLR configuration
generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'net.explorviz.code.analysis.antlr.generated', '-visitor']
    outputDirectory = file("build/generated-src/antlr/main/net/explorviz/code/analysis/antlr/generated")
}

// Ensure ANTLR generates before compilation
compileJava.dependsOn generateGrammarSource

// Regarding jandex dependency for each task
// https://github.com/kordamp/jandex-gradle-plugin/issues/9

tasks.named('checkstyleMain') {
    dependsOn 'jandex'
}

tasks.named('pmdMain') {
    dependsOn 'jandex', 'compileQuarkusGeneratedSourcesJava'
}

tasks.named('compileTestJava') {
    dependsOn 'jandex'
}

tasks.named('test') {
    dependsOn 'jandex'
}

tasks.named('quarkusDependenciesBuild') {
    dependsOn 'jandex'
}

tasks.withType(JavaCompile).configureEach {
    // Turn on all javac warnings except classfile and processing, which produces many false-positives about annotations
    options.compilerArgs << "-Xlint:all" << "-Xlint:-classfile" << "-Xlint:-processing"
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
}

